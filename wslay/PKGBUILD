arch=(x86_64)
conflicts=(wslay-git)
makedepends=(autoconf automake git libtool make pkg-config python-sphinx)
pkgdesc="The WebSocket library in C"
pkgname=wslay
pkgrel=1
pkgver=2011.12.20
provides=(wslay-git)

_configure_options=(
	--libexecdir=/usr/lib/wslay
	--localstatedir=/var/lib/wslay
	--prefix=/usr
	--sbindir=/usr/bin
	--sharedstatedir=/usr/share/wslay
	--sysconfdir=/etc
	--with-gnu-ld
)

sha384sums=(
	SKIP
)

source=(
	wslay::git+https://github.com/tatsuhiro-t/wslay
)

pkgver() {
	cd "${srcdir}/${pkgname}"
	git log -1 --date=short --format="%cd" --no-show-signature | sed s/-/./g
}

build() {
	cd "${srcdir}/${pkgname}"
	autoreconf -i
	automake
	autoconf
	./configure ${_configure_options[@]}
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make "DESTDIR=${pkgdir}" install
	rm -r "${pkgdir}/usr/share/man"
	for _file in "${pkgdir}/usr/bin/"*; do
		mv "${_file}" "${pkgdir}/usr/bin/${pkgname}_${_file##*/}"
	done
}
