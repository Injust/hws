arch=(x86_64)
backup=(etc/ssl/openssl.cnf)
depends=(perl)
makedepends=(make patch)
opensslver=1.1.0h
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"
pkgname=openssl
pkgrel=1
pkgver=${opensslver/[a-z]/.${opensslver//[0-9.]/}}

_configure_options=(
	"-Wa,--noexecstack ${CFLAGS} ${CPPFLAGS} ${LDFLAGS}"
	--libdir=lib
	--openssldir=/etc/ssl
	--prefix=/usr
	enable-ec_nistp_64_gcc_128
	enable-weak-ssl-ciphers
	linux-x86_64
	no-ssl3-method
	shared
)

sha384sums=(
	3b496b3f1592f56cee10a23bc2afc0ee82d8cf4a51a6ca15bc9922ed8080bf67cb00b4f1b47dcae2d8290ac9ec6ad268
	1731d532fbf2c0d5be497e1c57690554d6836934af39451c79e3eab140404b63925a742da1d05c85475da67835000a05
	805a9a42d285b2166a5b76c1c5ea3397ab40687de1cab30c116bfa9e99f10edaab30fbf30b816b86cd6889a8c10e6623
)

source=(
	openssl-${opensslver}.tar.gz::https://www.openssl.org/source/openssl-${opensslver}.tar.gz
	openssl__ca-dir.patch
	openssl__equal-preference.patch
)

prepare() {
	cd "${srcdir}/${pkgname}-${opensslver}"
	cat "${srcdir}/${pkgname}__"*.patch | patch -p1
}

build() {
	cd "${srcdir}/${pkgname}-${opensslver}"
	./Configure ${_configure_options[@]}
	make depend
	make
}

package() {
	cd "${srcdir}/${pkgname}-${opensslver}"
	make "DESTDIR=${pkgdir}" install_ssldirs install_sw
}
