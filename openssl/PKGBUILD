arch=(x86_64)
backup=(etc/ssl/openssl.cnf)
makedepends=(perl)
opensslver=1.1.0h
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security"
pkgname=openssl
pkgrel=1
pkgver=${opensslver/[a-z]/.${opensslver//[0-9.]/}}

_configure_options=(
	"-Wa,--noexecstack ${CFLAGS} ${CPPFLAGS} ${LDFLAGS}"
	--libdir=lib
	--openssldir=/etc/ssl
	--prefix=/usr
	enable-weak-ssl-ciphers
	linux-x86_64
	no-ssl3-method
	shared
)

sha384sums=(
	3b496b3f1592f56cee10a23bc2afc0ee82d8cf4a51a6ca15bc9922ed8080bf67cb00b4f1b47dcae2d8290ac9ec6ad268
	1731d532fbf2c0d5be497e1c57690554d6836934af39451c79e3eab140404b63925a742da1d05c85475da67835000a05
	9f0790fd230358466af5eb0d97ea7b05848eba89696a7e13068c36a5505fc0622a48aece89a865f4a896d186361b92ba
	f36bd82e9ca1e77a210f4ef1a1813da3759395b3aafad73913ce0fc173d269323bbc231563399b89adba5bf941874fb6
)

source=(
	openssl-${opensslver}.tar.gz::https://www.openssl.org/source/openssl-${opensslver}.tar.gz
	openssl__ca-dir.patch
	openssl__equal-preference.patch
	openssl__weak-ciphers.patch
)

prepare() {
	cd "${srcdir}/${pkgname}-${opensslver}"
	cat "${srcdir}/${pkgname}__"*.patch | patch -p1
}

build() {
	cd "${srcdir}/${pkgname}-${opensslver}"
	./Configure ${_configure_options[@]}
	make depend
	make
}

package() {
	cd "${srcdir}/${pkgname}-${opensslver}"
	make "DESTDIR=${pkgdir}" install_ssldirs install_sw
}
